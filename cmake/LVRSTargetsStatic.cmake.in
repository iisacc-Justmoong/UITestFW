# Static import targets for LVRS package consumers.

get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
    set(_IMPORT_PREFIX "")
endif()

if(NOT TARGET LVRS::LVRS)
    add_library(LVRS::LVRS STATIC IMPORTED)
    set_target_properties(LVRS::LVRS PROPERTIES
        IMPORTED_LOCATION "${_IMPORT_PREFIX}/@CMAKE_INSTALL_LIBDIR@/@CMAKE_STATIC_LIBRARY_PREFIX@LVRS@CMAKE_STATIC_LIBRARY_SUFFIX@"
        INTERFACE_COMPILE_DEFINITIONS "LVRS_USE_STATIC_QML_PLUGIN=1"
        INTERFACE_INCLUDE_DIRECTORIES "${_IMPORT_PREFIX}/@CMAKE_INSTALL_INCLUDEDIR@/LVRS"
        INTERFACE_LINK_LIBRARIES "Qt6::Quick;Qt6::QuickControls2;Qt6::Qml;Qt6::Svg;Qt6::Network"
        _qt_qml_module_installed_plugin_target "LVRSCoreplugin"
        _qt_qml_module_plugin_target "LVRSCoreplugin"
    )

    if(@LVRS_APPLE_AGL_SHIM_ACTIVE@)
        set_property(TARGET LVRS::LVRS APPEND PROPERTY INTERFACE_LINK_OPTIONS
            "-F${_IMPORT_PREFIX}/@CMAKE_INSTALL_LIBDIR@"
            "-Wl,-rpath,${_IMPORT_PREFIX}/@CMAKE_INSTALL_LIBDIR@"
        )
    endif()
endif()

set(_LVRS_QML_MODULE_PATH "${_IMPORT_PREFIX}/@LVRS_QML_MODULE_INSTALL_DIR@")
if(NOT TARGET LVRS::LVRSCoreplugin)
    set(_lvrs_plugin_location "")
    set(_lvrs_plugin_candidates
        "${_LVRS_QML_MODULE_PATH}/${CMAKE_STATIC_LIBRARY_PREFIX}LVRSCoreplugin${CMAKE_STATIC_LIBRARY_SUFFIX}"
        "${_LVRS_QML_MODULE_PATH}/LVRSCoreplugin${CMAKE_STATIC_LIBRARY_SUFFIX}"
        "${_LVRS_QML_MODULE_PATH}/${CMAKE_SHARED_LIBRARY_PREFIX}LVRSCoreplugin${CMAKE_SHARED_LIBRARY_SUFFIX}"
        "${_LVRS_QML_MODULE_PATH}/LVRSCoreplugin${CMAKE_SHARED_LIBRARY_SUFFIX}"
    )
    foreach(_lvrs_plugin_candidate IN LISTS _lvrs_plugin_candidates)
        if(EXISTS "${_lvrs_plugin_candidate}")
            set(_lvrs_plugin_location "${_lvrs_plugin_candidate}")
            break()
        endif()
    endforeach()

    if(_lvrs_plugin_location STREQUAL "")
        file(GLOB _lvrs_plugin_globbed
            "${_LVRS_QML_MODULE_PATH}/*LVRSCoreplugin*${CMAKE_STATIC_LIBRARY_SUFFIX}"
            "${_LVRS_QML_MODULE_PATH}/*LVRSCoreplugin*${CMAKE_SHARED_LIBRARY_SUFFIX}"
        )
        list(SORT _lvrs_plugin_globbed)
        if(_lvrs_plugin_globbed)
            list(GET _lvrs_plugin_globbed 0 _lvrs_plugin_location)
        endif()
    endif()

    if(NOT _lvrs_plugin_location STREQUAL "")
        add_library(LVRS::LVRSCoreplugin UNKNOWN IMPORTED)
        set_target_properties(LVRS::LVRSCoreplugin PROPERTIES
            IMPORTED_LOCATION "${_lvrs_plugin_location}"
            INTERFACE_LINK_LIBRARIES "LVRS::LVRS"
        )
    else()
        add_library(LVRS::LVRSCoreplugin INTERFACE IMPORTED)
        set_target_properties(LVRS::LVRSCoreplugin PROPERTIES
            INTERFACE_LINK_LIBRARIES "LVRS::LVRS"
        )
    endif()

    unset(_lvrs_plugin_candidate)
    unset(_lvrs_plugin_candidates)
    unset(_lvrs_plugin_globbed)
    unset(_lvrs_plugin_location)
endif()
unset(_LVRS_QML_MODULE_PATH)

unset(_IMPORT_PREFIX)
