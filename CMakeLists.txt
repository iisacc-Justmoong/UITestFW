cmake_minimum_required(VERSION 3.21)
project(LVRS VERSION 1.0.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

option(LVRS_BUILD_SHARED_LIBS "Build LVRS as shared library for dynamic consumption" ON)
option(LVRS_BUILD_EXAMPLES "Build runnable example applications" OFF)
option(LVRS_BUILD_TESTS "Build LVRS tests" OFF)
option(LVRS_INSTALL_QML_MODULE "Install LVRS QML module directory for downstream imports" ON)

option(LVRS_FORCE_X86_QT_TOOLS "Force Qt host tools to run under x86_64 (Rosetta)" OFF)
option(LVRS_ENABLE_APPLE_GL_LINK_SHIM "Provide a minimal AGL shim only for Apple linker compatibility" ON)
option(LVRS_ENFORCE_VULKAN "Fail configure if Vulkan-capable Qt/runtime are unavailable" ON)

set(LVRS_INSTALL_CMAKE_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/LVRS")
set(LVRS_QML_MODULE_INSTALL_BASE "${CMAKE_INSTALL_LIBDIR}/qt6/qml")
set(LVRS_QML_MODULE_INSTALL_DIR "${LVRS_QML_MODULE_INSTALL_BASE}/LVRS")
set(LVRS_APPLE_FRAMEWORK_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}")
set(LVRS_QML_IMPORT_PATH "${CMAKE_CURRENT_BINARY_DIR}")
set(LVRS_QML_MODULE_PATH "${CMAKE_CURRENT_BINARY_DIR}/LVRS")
set(LVRS_RUNTIME_PLATFORMS
    macos
    linux
    windows
    ios
    android
)
set(LVRS_APPLE_AGL_SHIM_ACTIVE OFF)
set(LVRS_APPLE_AGL_FRAMEWORK_ROOT "")

include(${CMAKE_CURRENT_LIST_DIR}/cmake/LVRSHelpers.cmake)

find_package(Qt6 6.5 REQUIRED COMPONENTS Quick QuickControls2 Qml Svg Network)
if(LVRS_BUILD_TESTS)
    find_package(Qt6 6.5 REQUIRED COMPONENTS Test)
endif()
find_package(Vulkan QUIET)

if(COMMAND qt_policy)
    qt_policy(SET QTP0004 NEW)
endif()

qt_standard_project_setup()

function(_lvrs_detect_qt_feature feature_name out_value out_header)
    set(_lvrs_qtgui_config_header "")
    if(DEFINED Qt6Gui_DIR)
        get_filename_component(_lvrs_qt_prefix "${Qt6Gui_DIR}/../../.." ABSOLUTE)
        get_filename_component(_lvrs_qt_lib_dir "${Qt6Gui_DIR}/../.." ABSOLUTE)
        set(_lvrs_qtgui_config_candidates
            "${_lvrs_qt_lib_dir}/QtGui.framework/Headers/qtgui-config.h"
            "${_lvrs_qt_prefix}/include/QtGui/qtgui-config.h"
            "${_lvrs_qt_prefix}/include/QtGui.framework/Headers/qtgui-config.h"
        )
        foreach(_lvrs_candidate_header IN LISTS _lvrs_qtgui_config_candidates)
            if(EXISTS "${_lvrs_candidate_header}")
                set(_lvrs_qtgui_config_header "${_lvrs_candidate_header}")
                break()
            endif()
        endforeach()
        unset(_lvrs_qtgui_config_candidates)
        unset(_lvrs_candidate_header)
        unset(_lvrs_qt_prefix)
        unset(_lvrs_qt_lib_dir)
    endif()

    if(NOT _lvrs_qtgui_config_header)
        set(${out_value} "" PARENT_SCOPE)
        set(${out_header} "" PARENT_SCOPE)
        return()
    endif()

    string(TOLOWER "${feature_name}" _lvrs_feature_name)
    file(STRINGS "${_lvrs_qtgui_config_header}" _lvrs_feature_line REGEX "^#define QT_FEATURE_${_lvrs_feature_name} ")
    if(_lvrs_feature_line MATCHES "(-?[0-9]+)$")
        set(_lvrs_feature_value "${CMAKE_MATCH_1}")
    else()
        set(_lvrs_feature_value "")
    endif()

    set(${out_value} "${_lvrs_feature_value}" PARENT_SCOPE)
    set(${out_header} "${_lvrs_qtgui_config_header}" PARENT_SCOPE)
endfunction()

set(LVRS_FORCED_BACKEND "default")
if(CMAKE_SYSTEM_NAME STREQUAL "Darwin" OR CMAKE_SYSTEM_NAME STREQUAL "iOS")
    set(LVRS_FORCED_BACKEND "metal")
elseif(WIN32 OR ANDROID OR CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(LVRS_FORCED_BACKEND "vulkan")
endif()

if(APPLE AND LVRS_ENABLE_APPLE_GL_LINK_SHIM)
    if(NOT EXISTS "/System/Library/Frameworks/AGL.framework/AGL")
        set(_uifw_framework_root "${CMAKE_CURRENT_BINARY_DIR}/frameworks")
        set(_uifw_agl_framework "${_uifw_framework_root}/AGL.framework")
        set(_uifw_agl_versions "${_uifw_agl_framework}/Versions")
        set(_uifw_agl_version_a "${_uifw_agl_versions}/A")
        set(_uifw_agl_binary "${_uifw_agl_version_a}/AGL")

        file(MAKE_DIRECTORY "${_uifw_agl_version_a}/Headers")
        file(MAKE_DIRECTORY "${_uifw_agl_version_a}/Resources")
        set(_uifw_agl_rebuild FALSE)
        if(EXISTS "${_uifw_agl_binary}")
            execute_process(
                COMMAND lipo -info "${_uifw_agl_binary}"
                OUTPUT_VARIABLE _uifw_agl_lipo_info
                RESULT_VARIABLE _uifw_agl_lipo_result
            )
            if(NOT _uifw_agl_lipo_result EQUAL 0)
                set(_uifw_agl_rebuild TRUE)
            elseif(CMAKE_OSX_ARCHITECTURES)
                foreach(_uifw_arch IN LISTS CMAKE_OSX_ARCHITECTURES)
                    if(NOT _uifw_agl_lipo_info MATCHES "${_uifw_arch}")
                        set(_uifw_agl_rebuild TRUE)
                    endif()
                endforeach()
            endif()
        else()
            set(_uifw_agl_rebuild TRUE)
        endif()

        if(_uifw_agl_rebuild)
            set(_uifw_agl_stub "${CMAKE_CURRENT_BINARY_DIR}/agl_stub.cpp")
            file(WRITE "${_uifw_agl_stub}" "void _uifw_dummy_agl(void) {}\n")
            set(_uifw_agl_arch_flags "")
            if(CMAKE_OSX_ARCHITECTURES)
                foreach(_uifw_arch IN LISTS CMAKE_OSX_ARCHITECTURES)
                    list(APPEND _uifw_agl_arch_flags -arch "${_uifw_arch}")
                endforeach()
            endif()
            execute_process(
                COMMAND "${CMAKE_CXX_COMPILER}" ${_uifw_agl_arch_flags} -dynamiclib "${_uifw_agl_stub}" -install_name "@rpath/AGL.framework/Versions/A/AGL" -o "${_uifw_agl_binary}"
                RESULT_VARIABLE _uifw_agl_build_result
            )
            if(NOT _uifw_agl_build_result EQUAL 0)
                message(FATAL_ERROR "Failed to build Apple GL link shim")
            endif()
            file(WRITE "${_uifw_agl_version_a}/Headers/AGL.h" "void _uifw_dummy_agl(void);\n")
            file(WRITE "${_uifw_agl_version_a}/Resources/Info.plist" "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n<!DOCTYPE plist PUBLIC \"-//Apple//DTD PLIST 1.0//EN\" \"http://www.apple.com/DTDs/PropertyList-1.0.dtd\">\n<plist version=\"1.0\"><dict><key>CFBundleName</key><string>AGL</string></dict></plist>\n")
        endif()
        if(NOT EXISTS "${_uifw_agl_versions}/Current")
            file(CREATE_LINK "A" "${_uifw_agl_versions}/Current" SYMBOLIC)
        endif()
        if(EXISTS "${_uifw_agl_framework}/AGL" OR IS_SYMLINK "${_uifw_agl_framework}/AGL")
            file(REMOVE "${_uifw_agl_framework}/AGL")
        endif()
        file(CREATE_LINK "Versions/Current/AGL" "${_uifw_agl_framework}/AGL" SYMBOLIC)
        if(EXISTS "${_uifw_agl_framework}/Headers" OR IS_SYMLINK "${_uifw_agl_framework}/Headers")
            file(REMOVE "${_uifw_agl_framework}/Headers")
        endif()
        if(EXISTS "${_uifw_agl_framework}/Headers")
            file(REMOVE_RECURSE "${_uifw_agl_framework}/Headers")
        endif()
        file(CREATE_LINK "Versions/Current/Headers" "${_uifw_agl_framework}/Headers" SYMBOLIC)
        if(EXISTS "${_uifw_agl_framework}/Resources" OR IS_SYMLINK "${_uifw_agl_framework}/Resources")
            file(REMOVE "${_uifw_agl_framework}/Resources")
        endif()
        if(EXISTS "${_uifw_agl_framework}/Resources")
            file(REMOVE_RECURSE "${_uifw_agl_framework}/Resources")
        endif()
        file(CREATE_LINK "Versions/Current/Resources" "${_uifw_agl_framework}/Resources" SYMBOLIC)
        set(LVRS_APPLE_AGL_SHIM_ACTIVE ON)
        set(LVRS_APPLE_AGL_FRAMEWORK_ROOT "${_uifw_framework_root}")
        set(CMAKE_FRAMEWORK_PATH "${_uifw_framework_root}" ${CMAKE_FRAMEWORK_PATH})
        add_link_options("-F${_uifw_framework_root}")
    endif()
endif()

if(APPLE AND LVRS_FORCE_X86_QT_TOOLS)
    function(_uifw_wrap_qt_tool target)
        get_target_property(_tool_path ${target} IMPORTED_LOCATION)
        if(NOT _tool_path)
            return()
        endif()
        string(REPLACE "::" "_" _tool_name "${target}")
        set(_wrapper "${CMAKE_CURRENT_BINARY_DIR}/${_tool_name}_x86_wrapper")
        file(WRITE "${_wrapper}" "#!/bin/sh\nexec arch -x86_64 \"${_tool_path}\" \"$@\"\n")
        file(CHMOD "${_wrapper}"
            PERMISSIONS
                OWNER_READ OWNER_WRITE OWNER_EXECUTE
                GROUP_READ GROUP_EXECUTE
                WORLD_READ WORLD_EXECUTE
        )
        set_property(TARGET ${target} PROPERTY IMPORTED_LOCATION "${_wrapper}")
    endfunction()

    _uifw_wrap_qt_tool(Qt6::rcc)
    if(TARGET Qt6::qmlcachegen)
        _uifw_wrap_qt_tool(Qt6::qmlcachegen)
    endif()
    if(TARGET Qt6::qmlsc)
        _uifw_wrap_qt_tool(Qt6::qmlsc)
    endif()
    if(TARGET Qt6::qmltyperegistrar)
        _uifw_wrap_qt_tool(Qt6::qmltyperegistrar)
    endif()
endif()

if(LVRS_BUILD_SHARED_LIBS)
    set(_lvrs_library_type SHARED)
else()
    set(_lvrs_library_type STATIC)
endif()

qt_add_library(LVRSCore ${_lvrs_library_type})
set_target_properties(LVRSCore PROPERTIES
    OUTPUT_NAME "LVRS"
    EXPORT_NAME "LVRS"
)
add_library(LVRS::LVRS ALIAS LVRSCore)

if(LVRS_APPLE_AGL_SHIM_ACTIVE)
    target_link_options(LVRSCore INTERFACE
        $<BUILD_INTERFACE:-F${LVRS_APPLE_AGL_FRAMEWORK_ROOT}>
        $<BUILD_INTERFACE:-Wl,-rpath,${LVRS_APPLE_AGL_FRAMEWORK_ROOT}>
        $<INSTALL_INTERFACE:-F$<INSTALL_PREFIX>/${LVRS_APPLE_FRAMEWORK_INSTALL_DIR}>
        $<INSTALL_INTERFACE:-Wl,-rpath,$<INSTALL_PREFIX>/${LVRS_APPLE_FRAMEWORK_INSTALL_DIR}>
    )
endif()

set(LVRS_STATIC_QML_PLUGIN_REQUIRED OFF)
if(NOT LVRS_BUILD_SHARED_LIBS)
    set(LVRS_STATIC_QML_PLUGIN_REQUIRED ON)
    target_compile_definitions(LVRSCore INTERFACE LVRS_USE_STATIC_QML_PLUGIN=1)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/backend/CMakeLists.txt)
include(${CMAKE_CURRENT_LIST_DIR}/qml/CMakeLists.txt)

target_include_directories(LVRSCore
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/io>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/fonts>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/platform>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/runtime>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/navigation>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/state>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/graphics>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/backend/text>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/LVRS>
)

qt_add_qml_module(LVRSCore
    URI LVRS
    VERSION 1.0
    RESOURCE_PREFIX "/qt/qml"
    OUTPUT_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/LVRS"
    SOURCES
        ${LVRS_BACKEND_SOURCES}
        ${LVRS_BACKEND_HEADERS}
    QML_FILES
        ${LVRS_QML_FILES}
    RESOURCES
        ${LVRS_RESOURCE_FILES}
)

if(TARGET LVRSCoreplugin)
    add_library(LVRSplugin ALIAS LVRSCoreplugin)
endif()
if(TARGET LVRSCoreplugin_init)
    add_library(LVRSplugin_init ALIAS LVRSCoreplugin_init)
endif()

if(LVRS_FORCED_BACKEND STREQUAL "vulkan")
    if(TARGET Vulkan::Headers)
        set(LVRS_VULKAN_TARGET Vulkan::Headers)
    elseif(TARGET Vulkan::Vulkan)
        set(LVRS_VULKAN_TARGET Vulkan::Vulkan)
    else()
        find_path(LVRS_VULKAN_INCLUDE_DIR NAMES vulkan/vulkan.h)
        if(NOT LVRS_VULKAN_INCLUDE_DIR)
            message(FATAL_ERROR "Vulkan headers are required on this platform, but vulkan/vulkan.h could not be found.")
        endif()
        add_library(LVRSVulkanHeaders INTERFACE)
        target_include_directories(LVRSVulkanHeaders INTERFACE "${LVRS_VULKAN_INCLUDE_DIR}")
        add_library(Vulkan::Headers ALIAS LVRSVulkanHeaders)
        set(LVRS_VULKAN_TARGET Vulkan::Headers)
        message(STATUS "Using header-only Vulkan dependency from: ${LVRS_VULKAN_INCLUDE_DIR}")
    endif()
endif()

if(LVRS_ENFORCE_VULKAN)
    if(LVRS_FORCED_BACKEND STREQUAL "vulkan")
        if(NOT TARGET Vulkan::Vulkan)
            message(FATAL_ERROR
                "LVRS_ENFORCE_VULKAN=ON requires a linkable Vulkan runtime target (Vulkan::Vulkan) on this platform. "
                "Current configure only resolved header-only Vulkan.")
        endif()

        _lvrs_detect_qt_feature(vulkan _lvrs_qt_vulkan_feature _lvrs_qtgui_config_header)
        if(_lvrs_qt_vulkan_feature STREQUAL "")
            message(FATAL_ERROR
                "LVRS_ENFORCE_VULKAN=ON but QT_FEATURE_vulkan could not be resolved from qtgui-config.h.")
        endif()

        if(_lvrs_qt_vulkan_feature LESS 0)
            message(FATAL_ERROR
                "LVRS_ENFORCE_VULKAN=ON but this Qt build has QT_FEATURE_vulkan=${_lvrs_qt_vulkan_feature} "
                "(${_lvrs_qtgui_config_header}). Install/rebuild Qt with Vulkan support.")
        endif()
    elseif(LVRS_FORCED_BACKEND STREQUAL "metal")
        _lvrs_detect_qt_feature(metal _lvrs_qt_metal_feature _lvrs_qtgui_config_header)
        if(_lvrs_qt_metal_feature STREQUAL "")
            message(FATAL_ERROR
                "LVRS_ENFORCE_VULKAN=ON but QT_FEATURE_metal could not be resolved from qtgui-config.h.")
        endif()

        if(_lvrs_qt_metal_feature LESS 0)
            message(FATAL_ERROR
                "LVRS_ENFORCE_VULKAN=ON and this platform requires Metal, but this Qt build has QT_FEATURE_metal=${_lvrs_qt_metal_feature} "
                "(${_lvrs_qtgui_config_header}). Install/rebuild Qt with Metal support.")
        endif()
    endif()
endif()

target_link_libraries(LVRSCore
    PUBLIC
        Qt6::Quick
        Qt6::QuickControls2
        Qt6::Qml
        Qt6::Svg
        Qt6::Network
)

if(LVRS_BUILD_EXAMPLES)
    add_subdirectory(example)
endif()

if(LVRS_BUILD_TESTS)
    enable_testing()
    add_subdirectory(tests)
endif()

if(LVRS_BUILD_SHARED_LIBS)
    install(TARGETS LVRSCore
        EXPORT LVRSTargets
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
        INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/LVRS
    )

    install(EXPORT LVRSTargets
        NAMESPACE LVRS::
        DESTINATION ${LVRS_INSTALL_CMAKE_DIR}
    )
else()
    install(TARGETS LVRSCore
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
        INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/LVRS
    )

    configure_file(
        "${CMAKE_CURRENT_LIST_DIR}/cmake/LVRSTargetsStatic.cmake.in"
        "${CMAKE_CURRENT_BINARY_DIR}/LVRSTargets.cmake"
        @ONLY
    )
    install(FILES
        "${CMAKE_CURRENT_BINARY_DIR}/LVRSTargets.cmake"
        DESTINATION ${LVRS_INSTALL_CMAKE_DIR}
    )
endif()

install(
    DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/backend/
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/LVRS/backend
    FILES_MATCHING
        PATTERN "*.h"
)

if(LVRS_INSTALL_QML_MODULE)
    install(
        DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/LVRS/"
        DESTINATION "${LVRS_QML_MODULE_INSTALL_DIR}"
    )
endif()

if(LVRS_APPLE_AGL_SHIM_ACTIVE)
    install(
        DIRECTORY "${LVRS_APPLE_AGL_FRAMEWORK_ROOT}/AGL.framework"
        DESTINATION "${LVRS_APPLE_FRAMEWORK_INSTALL_DIR}"
    )
endif()

configure_package_config_file(
    "${CMAKE_CURRENT_LIST_DIR}/cmake/LVRSConfig.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/LVRSConfig.cmake"
    INSTALL_DESTINATION ${LVRS_INSTALL_CMAKE_DIR}
    PATH_VARS
        LVRS_QML_MODULE_INSTALL_BASE
        LVRS_QML_MODULE_INSTALL_DIR
)

write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/LVRSConfigVersion.cmake"
    VERSION ${PROJECT_VERSION}
    COMPATIBILITY SameMajorVersion
)

install(FILES
    "${CMAKE_CURRENT_BINARY_DIR}/LVRSConfig.cmake"
    "${CMAKE_CURRENT_BINARY_DIR}/LVRSConfigVersion.cmake"
    "${CMAKE_CURRENT_LIST_DIR}/cmake/LVRSHelpers.cmake"
    "${CMAKE_CURRENT_LIST_DIR}/cmake/LVRSAppEntryPoint.cpp.in"
    DESTINATION ${LVRS_INSTALL_CMAKE_DIR}
)
